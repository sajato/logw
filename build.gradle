subprojects {

    apply plugin: 'java'
    apply from: '../codeQuality.gradle'

    group = 'eu.sajato.logw'
    version = '0.2.0-SNAPSHOT'
//    version = '0.2.0'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-integration', version: '1.3'
        testCompile group: 'org.jmockit', name: 'jmockit', version: '1.19'
    }

    jar {
        manifest.attributes 'Implementation-Title': project.name
        manifest.attributes 'Implementation-Vendor': 'https://github.com/sajato/logw'
        manifest.attributes 'Implementation-Version': version
    }
}

configure(subprojects - project(':benchmark')) {

    apply plugin: 'maven'
    apply plugin: 'signing'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            // TODO Use same pom.project definition for uploadArchives and install.
            pom.project {

                // TODO Use description from subproject.
                // description project.description

                url 'https://github.com/sajato/logw'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com:sajato/logw.git'
                    developerConnection 'scm:git:git@github.com:sajato/logw.git'
                    url 'git@github.com:sajato/logw.git'
                }

                issueManagement {
                    system 'github'
                    url "https://github.com/sajato/logw/issues"
                }
            }
        }
    }

    install {
        repositories.mavenInstaller {

            // TODO Use same pom.project definition for uploadArchives and install.
            pom.project {

                // TODO Use description from subproject.
                // description project.description

                url 'https://github.com/sajato/logw'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com:sajato/logw.git'
                    developerConnection 'scm:git:git@github.com:sajato/logw.git'
                    url 'git@github.com:sajato/logw.git'
                }

                issueManagement {
                    system 'github'
                    url "https://github.com/sajato/logw/issues"
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}
